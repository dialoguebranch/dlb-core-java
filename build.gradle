plugins {
	id 'base'
	id 'java'
	id 'java-library'
	id 'application'
	id 'distribution'
}

group = 'com.dialoguebranch'
version = '1.1.0'

application {
	mainClassName = "com.dialoguebranch.cli.CommandLineRunner"
}

run {
	standardInput = System.in
}

repositories {
	mavenCentral()
}

base {
	archivesName = "dlb-core-java"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withJavadocJar()
	withSourcesJar()
}

dependencies {
	api "nl.rrd:rrd-utils:3.0.0"
	api 'com.fasterxml.jackson.module:jackson-modules-java8:2.13.4'
	testImplementation 'junit:junit:4.13.2'
}

jar {
	manifest {
		attributes 'Main-Class': 'com.dialoguebranch.cli.CommandLineRunner'
		attributes 'Class-Path': configurations.compileClasspath.collect({ it.name }).join(' ')
	}
}

distributions {
	main {
		distributionBaseName = base.archivesName
		contents {
			from configurations.compileClasspath
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar

javadoc {
	destinationDir = file("${buildDir}/javadoc/")
	include 'com/dialoguebranch/**'
	options.source = "17"
}