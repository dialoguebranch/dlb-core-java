plugins {
	id 'base'
	id 'java'
	id 'java-library'
	id 'application'
	id 'distribution'
}

group = 'com.dialoguebranch'
archivesBaseName = 'dlb-core-java'
version = '1.2.3'

repositories {
	mavenCentral()
}

application {
	mainClassName = "com.dialoguebranch.cli.CommandLineRunner"
}

run {
	standardInput = System.in
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	source = sourceSets.main.allJava
	destinationDir = reporting.file("javadoc")
	include 'com/dialoguebranch/**'
	options.source = "17"
	options.addStringOption('Xmaxwarns', '1000')
}

dependencies {
	api "nl.rrd:rrd-utils:3.0.3"
	api 'com.fasterxml.jackson.module:jackson-modules-java8:2.13.4'
	testImplementation 'junit:junit:4.13.2'
	// SLF4J Logging Framework
	api 'org.slf4j:slf4j-api:2.0.12'

	// SLF4J JDK includes the API, but when replaced with a different logging provider, API must be included specifically
	api 'org.slf4j:slf4j-jdk14:2.0.13'
}

jar {
	manifest {
		attributes 'Main-Class': 'com.dialoguebranch.cli.CommandLineRunner'
		attributes 'Class-Path': configurations.compileClasspath.collect({ it.name }).join(' ')
	}
}

distributions {
	main {
		distributionBaseName = archivesBaseName
		contents {
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar