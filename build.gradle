plugins {
	id 'base'
	id 'java'
	id 'java-library'
	id 'application'
	id 'distribution'
}

group = 'com.dialoguebranch'
version = '1.2.5'

base {
	archivesName = 'dlb-core-java'
}

repositories {
	mavenCentral()
}

application {
	mainClass = "com.dialoguebranch.cli.CommandLineRunner"
}

run {
	standardInput = System.in
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	if (project.properties['buildEnv'] == null || !project.properties['buildEnv'].equals('dev')) {
		withJavadocJar()
		withSourcesJar()
	}
}

javadoc {
	source = sourceSets.main.allJava
	destinationDir = reporting.file("javadoc")
	include 'com/dialoguebranch/**'
	options.source = "17"
	options.addStringOption('Xmaxwarns', '100')
}

dependencies {
	api "nl.rrd:rrd-utils:3.0.3"
	api 'com.fasterxml.jackson.module:jackson-modules-java8:2.18.0-rc1'
	testImplementation 'junit:junit:4.13.2'
	api 'org.slf4j:slf4j-api:2.0.16'
}

jar {
	manifest {
		attributes 'Main-Class': 'com.dialoguebranch.cli.CommandLineRunner'
		attributes 'Class-Path': configurations.compileClasspath.collect({ it.name }).join(' ')
	}
}

distributions {
	main {
		distributionBaseName = base.archivesName
		contents {
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar